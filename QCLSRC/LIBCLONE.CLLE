PGM        PARM(&SRCLIB)
COPYRIGHT  TEXT('Andrea Ribuoli Â© 2022 - PASERIE Local Init')
DCL        VAR(&SRCLIB)    TYPE(*CHAR) LEN(10)
DCL        VAR(&RETLIB)    TYPE(*CHAR) LEN(10)
DCL        VAR(&SRCMBR)    TYPE(*CHAR) LEN(10)
DCL        VAR(&LASTMBR)   TYPE(*CHAR) LEN(10)
DCL        VAR(&SRCTYPE)   TYPE(*CHAR) LEN(10)
DCL        VAR(&MSGID)     TYPE(*CHAR) LEN(7)
DCL        VAR(&LOCALPATH) TYPE(*CHAR) LEN(20)
DCL        VAR(&FROMMBR)   TYPE(*CHAR) LEN(100)
DCL        VAR(&TOSTMF)    TYPE(*CHAR) LEN(100)
DCL        VAR(&DIR)       TYPE(*CHAR) LEN(50)
DCL        VAR(&RTCD)      TYPE(*INT)  LEN(4)
DCL        VAR(&R_OK)      TYPE(*INT)  LEN(4)  VALUE(4)
DCL        VAR(&NL)        TYPE(*CHAR) LEN(1)  VALUE(X'00')
DCL        VAR(&LOOPEXT)   TYPE(*LGL)          VALUE('1')
DCL        VAR(&LOOP)      TYPE(*LGL)
DCL        VAR(&SPC_NAME)  TYPE(*CHAR) LEN(20) VALUE('SRCPFS    QTEMP     ')
DCL        VAR(&SPC_NAME2) TYPE(*CHAR) LEN(20) VALUE('PFATTR    QTEMP     ')
DCL        VAR(&SPC_SIZE)  TYPE(*INT)  LEN(4)  VALUE(1)
DCL        VAR(&SPC_SIZE2) TYPE(*INT)  LEN(4)  VALUE(8192)
DCL        VAR(&SPC_INIT)  TYPE(*CHAR) LEN(1)  VALUE(X'00')
DCL        VAR(&BLANKS)    TYPE(*CHAR) LEN(50)
DCL        VAR(&QFN)       TYPE(*CHAR) LEN(20)
DCL        VAR(&QFILE)     TYPE(*CHAR) LEN(10) STG(*DEFINED) DEFVAR(&QFN  1)
DCL        VAR(&QLIB)      TYPE(*CHAR) LEN(10) STG(*DEFINED) DEFVAR(&QFN  11)
DCL        VAR(&QRETFN)    TYPE(*CHAR) LEN(20)
DCL        VAR(&QREQFN)    TYPE(*CHAR) LEN(20)
DCL        VAR(&REQFILE)   TYPE(*CHAR) LEN(10) STG(*DEFINED) DEFVAR(&QREQFN  1)
DCL        VAR(&REQLIB)    TYPE(*CHAR) LEN(10) STG(*DEFINED) DEFVAR(&QREQFN  11)
DCL        VAR(&OBJTYPE)   TYPE(*CHAR) LEN(10) VALUE('*FILE')
DCL        VAR(&ERRCDE)    TYPE(*CHAR) LEN(16) VALUE(X'00000010')
DCL        VAR(&BYTPRV)    TYPE(*INT)  STG(*DEFINED) LEN(4) DEFVAR(&ERRCDE)
DCL        VAR(&BYTAVL)    TYPE(*INT)  STG(*DEFINED) LEN(4) DEFVAR(&ERRCDE 5)
DCL        VAR(&EMSGID)    TYPE(*CHAR) STG(*DEFINED) LEN(7) DEFVAR(&ERRCDE 9)
DCL        VAR(&ERRCDE2)   TYPE(*CHAR) LEN(16) VALUE(X'00000010')
DCL        VAR(&BYTPRV2)   TYPE(*INT)  STG(*DEFINED) LEN(4) DEFVAR(&ERRCDE2)
DCL        VAR(&BYTAVL2)   TYPE(*INT)  STG(*DEFINED) LEN(4) DEFVAR(&ERRCDE2 5)
DCL        VAR(&EMSGID2)   TYPE(*CHAR) STG(*DEFINED) LEN(7) DEFVAR(&ERRCDE2 9)
DCL        VAR(&SPCPTR)    TYPE(*PTR)
DCL        VAR(&LISTHDR)   TYPE(*CHAR) STG(*BASED)   LEN(192) BASPTR(&SPCPTR)
DCL        VAR(&LISTSTS)   TYPE(*CHAR) STG(*DEFINED) LEN(1) DEFVAR(&LISTHDR 104)
DCL        VAR(&PARHDROFS) TYPE(*INT)  STG(*DEFINED) LEN(4) DEFVAR(&LISTHDR 109)
DCL        VAR(&LSTENOFS)  TYPE(*INT)  STG(*DEFINED) LEN(4) DEFVAR(&LISTHDR 125)
DCL        VAR(&LSTENTNBR) TYPE(*INT)  STG(*DEFINED) LEN(4) DEFVAR(&LISTHDR 133)
DCL        VAR(&LSTENTSIZ) TYPE(*INT)  STG(*DEFINED) LEN(4) DEFVAR(&LISTHDR 137)
DCL        VAR(&SPCPTR2)   TYPE(*PTR)
DCL        VAR(&LISTHDR2)  TYPE(*CHAR) STG(*BASED)   LEN(8192) BASPTR(&SPCPTR2)
DCL        VAR(&BITS)      TYPE(*UINT) STG(*DEFINED) LEN(2) DEFVAR(&LISTHDR2 9)
DCL        VAR(&BITSMASK)  TYPE(*UINT)               LEN(2) VALUE(2048)
DCL        VAR(&LSTPTR)    TYPE(*PTR)
DCL        VAR(&LSTENT)    TYPE(*CHAR) STG(*BASED)   LEN(100) BASPTR(&LSTPTR)
DCL        VAR(&SRCFILE)   TYPE(*CHAR) STG(*DEFINED) LEN(10) DEFVAR(&LSTENT 1)
DCL        VAR(&CONTIN)    TYPE(*CHAR) STG(*DEFINED) LEN(20) DEFVAR(&LSTENT 11)
DCL        VAR(&ITER)      TYPE(*INT)  LEN(4)
/*   ----  converted from RPG ------------------------  */
/*     DCHG_ATTR         DS                             */
/*     D NBR_ATTR                       9B 0 INZ(1)     */
/*     D ATTR_KEY                       9B 0 INZ(3)     */
/*     D DATA_SIZE                      9B 0 INZ(1)     */
/*     D ATTR_DATA                      1    INZ('1')   */
DCL        VAR(&CHG_ATTR)  TYPE(*CHAR) LEN(13) VALUE(X'000000010000000300000001F1')
DCL        VAR(&NBR_ATTR)  TYPE(*INT)  STG(*DEFINED) LEN(4) DEFVAR(&CHG_ATTR  1)
DCL        VAR(&ATTR_KEY)  TYPE(*INT)  STG(*DEFINED) LEN(4) DEFVAR(&CHG_ATTR  5)
DCL        VAR(&DATA_SIZE) TYPE(*INT)  STG(*DEFINED) LEN(4) DEFVAR(&CHG_ATTR  9)
DCL        VAR(&ATTR_DATA) TYPE(*CHAR) STG(*DEFINED) LEN(1) DEFVAR(&CHG_ATTR 13)

MONMSG     MSGID(CPF9801) EXEC(GOTO CMDLBL(CHECK1))
MONMSG     MSGID(CPF9802) EXEC(GOTO CMDLBL(CHECK2))
CHKOBJ     OBJ(QSYS/&SRCLIB) OBJTYPE(*LIB) AUT(*READ)
CHGVAR     VAR(&LOCALPATH)   VALUE('./' *CAT &SRCLIB *TCAT &NL)
CALLPRC    PRC('access') PARM((&LOCALPATH) (&R_OK *BYVAL)) RTNVAL(&RTCD)
IF         COND(&RTCD = 0) THEN(DO)
SNDPGMMSG  MSG('Directory or file ' *CAT &SRCLIB *TCAT +
               ' already present in current directory')
RETURN
ENDDO
MKDIR DIR(&SRCLIB)

CALL       PGM(QUSCRTUS) PARM(&SPC_NAME 'QUSLOBJ' &SPC_SIZE X'00' +
                              '*ALL' &BLANKS '*YES' &ERRCDE '*USER')
IF         COND(&BYTAVL *GT 0) THEN(DO)
SNDPGMMSG  MSG('Failure with QUSCRTUS (' *CAT &EMSGID *CAT ')') +
           TOPGMQ(*EXT)
RETURN
ENDDO

CALL       PGM(QUSCUSAT) PARM(&RETLIB &SPC_NAME &CHG_ATTR &ERRCDE)
IF         COND(&BYTAVL *GT 0) THEN(DO)
SNDPGMMSG  MSG('Failure with QUSCUSAT (' *CAT &EMSGID *CAT ')') +
           TOPGMQ(*EXT)
RETURN
ENDDO

CALL       PGM(QUSPTRUS) PARM(&SPC_NAME &SPCPTR &ERRCDE)
IF         COND(&BYTAVL *GT 0) THEN(DO)
SNDPGMMSG  MSG('Failure with QUSPTRUS (' *CAT &EMSGID *CAT ')') +
           TOPGMQ(*EXT)
RETURN
ENDDO


CALL       PGM(QUSCRTUS) PARM(&SPC_NAME2 'QDBRTVFD' +
                          &SPC_SIZE2 X'00' '*ALL' &BLANKS '*YES' +
                          &ERRCDE2 '*USER')
IF         COND(&BYTAVL2 *GT 0) THEN(DO)
SNDPGMMSG  MSG('Failure with QUSCRTUS (' *CAT &EMSGID2 *CAT ')') +
           TOPGMQ(*EXT)
RETURN
ENDDO

CALL       PGM(QUSCUSAT) PARM(&RETLIB &SPC_NAME2 &CHG_ATTR &ERRCDE2)
IF         COND(&BYTAVL2 *GT 0) THEN(DO)
SNDPGMMSG  MSG('Failure with QUSCUSAT (' *CAT &EMSGID2 *CAT ')') +
           TOPGMQ(*EXT)
RETURN
ENDDO

CALL       PGM(QUSPTRUS) PARM(&SPC_NAME2 &SPCPTR2 &ERRCDE2)
IF         COND(&BYTAVL2 *GT 0) THEN(DO)
SNDPGMMSG  MSG('Failure with QUSPTRUS (' *CAT &EMSGID2 *CAT ')') +
           TOPGMQ(*EXT)
RETURN
ENDDO

CHGVAR     VAR(&REQFILE) VALUE('*ALL')
CHGVAR     VAR(&REQLIB)  VALUE(&SRCLIB)
CALL       PGM(QUSLOBJ)  PARM(&SPC_NAME 'OBJL0100' &QREQFN &OBJTYPE &ERRCDE)
IF         COND(&BYTAVL *GT 0) THEN(DO)
SNDPGMMSG  MSG('Failure with QUSLOBJ (' *CAT &EMSGID *CAT ')') +
           TOPGMQ(*EXT)
RETURN
ENDDO
CHGVAR     VAR(&LSTPTR) VALUE(&SPCPTR)
CHGVAR     VAR(%OFFSET(&LSTPTR)) VALUE(%OFFSET(&LSTPTR) + &LSTENOFS)

CALLSUBR SUBR(SLSRCFILES)
RETURN

CHECK1:    SNDPGMMSG  MSG('Library ' *CAT &SRCLIB *TCAT ' does not exist')
RETURN
CHECK2:    SNDPGMMSG  MSG('Not authorized to library ' *CAT &SRCLIB)
RETURN
CHECK3:    SNDPGMMSG  MSG('Unusual error with library ' *CAT &SRCLIB)
RETURN

SUBR SUBR(SLSRCFILES)
 DOFOR VAR(&ITER) FROM(1) TO(&LSTENTNBR)
  CHGVAR VAR(&DIR) VALUE(&SRCLIB *TCAT '/' *CAT &SRCFILE)
  CALLSUBR SUBR(CPYSRCFILE)
  CHGVAR     VAR(%OFFSET(&LSTPTR)) VALUE(%OFFSET(&LSTPTR) + &LSTENTSIZ)
 ENDDO
ENDSUBR

SUBR SUBR(CPYSRCFILE)
CHGVAR VAR(&LOOP)  VALUE('1')
CHGVAR VAR(&QFILE) VALUE(&SRCFILE)
CHGVAR VAR(&QLIB)  VALUE(&SRCLIB)
CALL QDBRTVFD PARM(&LISTHDR2 &SPC_SIZE2 &QRETFN 'FILD0100' &QFN '*FIRST    ' +
                   '0' '*LCL      ' '*INT      ' &ERRCDE2)
IF         COND(&BYTAVL2 *GT 0) THEN(DO)
/* SNDPGMMSG  MSG('Failure with QDBRTVFD (' *CAT &EMSGID2 *CAT ')') */
IF COND(&EMSGID2 = 'CPF3C23') THEN(RTNSUBR)
RETURN
ENDDO

CHGVAR VAR(&BITS) VALUE(&BITS * 16)
CHGVAR VAR(&BITS) VALUE(&BITS / 32768)
IF         COND(&BITS *NE 1) THEN(RTNSUBR)

RTVMBRD FILE(&SRCLIB/&SRCFILE) MBR(*FIRSTMBR *SAME) RTNMBR(&SRCMBR) SRCTYPE(&SRCTYPE)
MONMSG MSGID(CPF0000) EXEC(RTNSUBR)
MKDIR DIR(&DIR)
DOWHILE COND(&LOOP)
 SNDPGMMSG MSG('Processing' *BCAT &SRCLIB *TCAT ' ' *CAT &SRCFILE *TCAT ' ' *CAT &SRCMBR)
 CHGVAR VAR(&FROMMBR) VALUE('/QSYS.LIB/' *CAT &SRCLIB *TCAT '.LIB/' *CAT &SRCFILE +
                          *TCAT '.FILE/' *CAT &SRCMBR *TCAT '.MBR')
 CHGVAR VAR(&TOSTMF) VALUE('./' *CAT &SRCLIB  *TCAT '/' +
                                *CAT &SRCFILE *TCAT '/' +
                                *CAT &SRCMBR  *TCAT '.' *CAT &SRCTYPE)
 CPYTOSTMF FROMMBR(&FROMMBR) TOSTMF(&TOSTMF) STMFCCSID(1208) ENDLINFMT(*LF)
 MONMSG MSGID(CPDA082) EXEC(LEAVE)
 CHGVAR VAR(&LASTMBR) VALUE(&SRCMBR)
 RTVMBRD FILE(&SRCLIB/&SRCFILE) MBR(&LASTMBR *NEXT) RTNMBR(&SRCMBR) SRCTYPE(&SRCTYPE)
 MONMSG MSGID(CPF3049) EXEC(LEAVE)
ENDDO
ENDSUBR

ENDPGM
