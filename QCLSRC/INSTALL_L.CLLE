PGM PARM(&FROMPATH &DEVOPT)
DCLPRCOPT  LOG(*NO) ALWRTVSRC(*NO)
COPYRIGHT  TEXT('Andrea Ribuoli Â© 2022 - PASERIE Local Inst')
DCL VAR(&FROMPATH) TYPE(*CHAR) LEN(100)
DCL VAR(&DEVOPT) TYPE(*CHAR) LEN(1)
DCL VAR(&SPOOL)  TYPE(*CHAR) LEN(6) VALUE(*NONE)
DCL VAR(&LOOP) TYPE(*LGL) VALUE('1')
DCL VAR(&FROMSTMF) TYPE(*CHAR) LEN(150)
DCL VAR(&TOMBR) TYPE(*CHAR) LEN(150)
DCL VAR(&MODE) TYPE(*INT) LEN(4) VALUE(7)
DCL VAR(&OFLAG) TYPE(*INT) LEN(4) VALUE(16778570)
DCL VAR(&FD) TYPE(*INT) LEN(4)
DCL VAR(&NOFB) TYPE(*UINT) LEN(4) VALUE(81)
DCL VAR(&RTCD) TYPE(*INT) LEN(4)
DCL VAR(&ROW) TYPE(*CHAR) LEN(81)
DCL VAR(&LINEFEED) TYPE(*CHAR) LEN(1) VALUE(X'25')
DCLF FILE(QTEMP/GUIDE00F)
IF COND(&DEVOPT = 'B') THEN(DO)
CHGVAR VAR(&SPOOL) VALUE(*PRINT)
CHGVAR VAR(&DEVOPT) VALUE('N')
ENDDO
CRTSRCPF FILE(QTEMP/QDDSPF)
CHGVAR VAR(&TOMBR) VALUE('/QSYS.LIB/QTEMP.LIB/QDDSPF.FILE/GUIDE00F.MBR')
CALLPRC PRC('open') PARM((&TOMBR) (&OFLAG *BYVAL) (&MODE *BYVAL)) RTNVAL(&FD)
CHGVAR VAR(&ROW) VALUE('                R GUIDE')
CHGVAR VAR(&ROW) VALUE(%SST(&ROW 1 80) *CAT &LINEFEED)
CALLPRC PRC('write') PARM((&FD *BYVAL) (&ROW) (&NOFB *BYVAL)) RTNVAL(&RTCD)
CHGVAR VAR(&ROW) VALUE('                  SRCFILE       10A')
CHGVAR VAR(&ROW) VALUE(%SST(&ROW 1 80) *CAT &LINEFEED)
CALLPRC PRC('write') PARM((&FD *BYVAL) (&ROW) (&NOFB *BYVAL)) RTNVAL(&RTCD)
CHGVAR VAR(&ROW) VALUE('                  SRCMBR        10A')
CHGVAR VAR(&ROW) VALUE(%SST(&ROW 1 80) *CAT &LINEFEED)
CALLPRC PRC('write') PARM((&FD *BYVAL) (&ROW) (&NOFB *BYVAL)) RTNVAL(&RTCD)
CHGVAR VAR(&ROW) VALUE('                  SRCTYPE       10A')
CHGVAR VAR(&ROW) VALUE(%SST(&ROW 1 80) *CAT &LINEFEED)
CALLPRC PRC('write') PARM((&FD *BYVAL) (&ROW) (&NOFB *BYVAL)) RTNVAL(&RTCD)
CHGVAR VAR(&ROW) VALUE('                  SRCDESC       50A')
CHGVAR VAR(&ROW) VALUE(%SST(&ROW 1 80) *CAT &LINEFEED)
CALLPRC PRC('write') PARM((&FD *BYVAL) (&ROW) (&NOFB *BYVAL)) RTNVAL(&RTCD)
CALLPRC PRC('close') PARM((&FD *BYVAL)) RTNVAL(&RTCD)
CRTPF FILE(QTEMP/GUIDE00F) SRCFILE(QTEMP/QDDSPF) OPTION(*NOSRC *NOLIST)
CHGVAR VAR(&FROMSTMF) VALUE(&FROMPATH *TCAT '/GUIDANCE.TXT')
CHGVAR VAR(&TOMBR) VALUE('/QSYS.LIB/QTEMP.LIB/QDDSPF.FILE/GUIDANCE.MBR')
CPYFRMSTMF FROMSTMF(&FROMSTMF) TOMBR(&TOMBR)
CPYF FROMFILE(QTEMP/QDDSPF) TOFILE(QTEMP/GUIDE00F) +
  FROMMBR(GUIDANCE) TOMBR(GUIDE00F) MBROPT(*ADD) FMTOPT(*CVTSRC)
DLTF FILE(QTEMP/QDDSPF)
DOWHILE COND(&LOOP)
 RCVF
 MONMSG MSGID(CPF0864) EXEC(LEAVE)
 CRTSRCPF FILE(QTEMP/&SRCFILE) RCDLEN(132)
 MONMSG MSGID(CPF0000)
 CHGVAR VAR(&FROMSTMF) VALUE(&FROMPATH *TCAT '/' *CAT &SRCFILE *TCAT '/' +
                           *CAT &SRCMBR *TCAT '.' *CAT &SRCTYPE)
 CHGVAR VAR(&TOMBR) VALUE('/QSYS.LIB/QTEMP.LIB/' *CAT &SRCFILE +
                          *TCAT '.FILE/' *CAT &SRCMBR *TCAT '.MBR')
 CPYFRMSTMF FROMSTMF(&FROMSTMF) TOMBR(&TOMBR)
 MONMSG MSGID(CPFA095) EXEC(ITERATE)
 CHGPFM FILE(QTEMP/&SRCFILE) MBR(&SRCMBR) SRCTYPE(&SRCTYPE) TEXT(&SRCDESC)
ENDDO
CRTBNDCL PGM(QTEMP/BUILD) SRCFILE(QTEMP/QCLSRC) SRCMBR(BUILD) +
         OUTPUT(&SPOOL) ALWRTVSRC(*NO)
MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(FINE))
CALL PGM(QTEMP/BUILD) PARM(&DEVOPT)
MONMSG MSGID(CPF0000)
FINE: ENDPGM
