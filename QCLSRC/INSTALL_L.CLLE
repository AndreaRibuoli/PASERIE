PGM PARM(&FROMPATH &TGTLIB &DEVOPT &TGTRLS &OMISSIBLE)
DCLPRCOPT  LOG(*NO) ALWRTVSRC(*NO)
COPYRIGHT  TEXT('Andrea Ribuoli Â© 2022 - PASERIE Local Inst')
DCL VAR(&FROMPATH) TYPE(*CHAR) LEN(100)
DCL VAR(&TGTLIB)    TYPE(*CHAR) LEN(10)
DCL VAR(&DEVOPT) TYPE(*CHAR) LEN(1)
DCL VAR(&TGTRLS)    TYPE(*CHAR) LEN(10)
DCL VAR(&OMISSIBLE) TYPE(*CHAR) LEN(1)
DCL VAR(&VERBOSE)   TYPE(*CHAR) LEN(1)
DCL VAR(&SPOOL)  TYPE(*CHAR) LEN(6) VALUE(*NONE)
DCL VAR(&LOGCLPGM) TYPE(*CHAR) LEN(5) VALUE(*SAME)
DCL VAR(&LOG)    TYPE(*CHAR) LEN(7) VALUE(*SAME)
DCL VAR(&LOOP) TYPE(*LGL) VALUE('1')
DCL VAR(&FROMSTMF) TYPE(*CHAR) LEN(150)
DCL VAR(&TOMBR) TYPE(*CHAR) LEN(150)
DCL VAR(&MODE) TYPE(*INT) LEN(4) VALUE(7)
DCL VAR(&OFLAG) TYPE(*INT) LEN(4) VALUE(16778570)
DCL VAR(&FD) TYPE(*INT) LEN(4)
DCL VAR(&NOFB) TYPE(*UINT) LEN(4) VALUE(81)
DCL VAR(&RTCD) TYPE(*INT) LEN(4)
DCL VAR(&ROW) TYPE(*CHAR) LEN(81)
DCL VAR(&NL) TYPE(*CHAR) LEN(1) VALUE(X'00')
DCL VAR(&X15) TYPE(*CHAR) LEN(1) VALUE(X'15')
DCL VAR(&LINEFEED) TYPE(*CHAR) LEN(1) VALUE(X'25')
DCL VAR(&MSGDTA) TYPE(*CHAR) LEN(128)
DCLF FILE(QTEMP/GUIDE00F)
             DCL        VAR(&LANG_DIR) TYPE(*CHAR) LEN(5) VALUE('     ')
             DCL        VAR(&USELANGDIR) TYPE(*CHAR) LEN(5) VALUE('     ')
             DCL        VAR(&LANG_FC) TYPE(*CHAR) LEN(4)
             DCL        VAR(&OS_DIR) TYPE(*CHAR) LEN(7) VALUE('       ')
             DCL        VAR(&USEOSDIR) TYPE(*CHAR) LEN(7) VALUE('       ')
             DCL        VAR(&CUR_OSV) TYPE(*CHAR) LEN(6) VALUE('      ')
             DCL        VAR(&CUR_OSV_3) TYPE(*CHAR) LEN(3) VALUE('   ')
             DCL        VAR(&CUR_OSV_D) TYPE(*DEC) LEN(3 0) VALUE(0)
             DCL        VAR(&IMODE)     TYPE(*CHAR) LEN(11)  VALUE('rr+')
             DCL        VAR(&QTMPMBR)   TYPE(*CHAR) LEN(61)
             DCL        VAR(&DOT)         TYPE(*CHAR) LEN(1)   VALUE('.')
             DCL        VAR(&IPOS)        TYPE(*INT)  LEN(2)
             DCL        VAR(&IRC)         TYPE(*INT)  LEN(4)
             DCL        VAR(&FP)          TYPE(*PTR)  ADDRESS(*NULL)
             DCL        VAR(&RFILE)       TYPE(*CHAR) LEN(336) STG(*BASED)   BASPTR(&FP)
             DCL        VAR(&F10_BUFLEN)  TYPE(*UINT) LEN(4)   STG(*DEFINED) DEFVAR(&RFILE 193) 
             DCL        VAR(&RIOFB_P)     TYPE(*PTR)  ADDRESS(*NULL)
             DCL        VAR(&RIOFB)       TYPE(*CHAR) LEN(64)  STG(*BASED)   BASPTR(&RIOFB_P)
             DCL        VAR(&R04_NUMBYT) TYPE(*INT)  LEN(4)   STG(*DEFINED) DEFVAR(&RIOFB 37) 
             DCL        VAR(&RAW_ROW)     TYPE(*CHAR) LEN(312) 
             DCL        VAR(&SRCDTA)      TYPE(*CHAR) LEN(300) STG(*DEFINED) DEFVAR(&RAW_ROW 13)  
             DCL        VAR(&OPTS)        TYPE(*INT)  LEN(4)   VALUE(0)
             CHGVAR     VAR(&IMODE) VALUE(&IMODE *TCAT &NL)             
             RTVDTAARA  DTAARA(QGPL/QSS1MRI (26 4)) RTNVAR(&LANG_FC)
             MONMSG     MSGID(CPF0000)
             RTVDTAARA  DTAARA(PASE_TEST (26 4)) RTNVAR(&LANG_FC)
             MONMSG     MSGID(CPF0000)
             RTVDTAARA  DTAARA(QGPL/QSS1MRI (1 6)) RTNVAR(&CUR_OSV)
             MONMSG     MSGID(CPF0000)
             RTVDTAARA  DTAARA(PASE_TEST (1 6)) RTNVAR(&CUR_OSV)
             MONMSG     MSGID(CPF0000)
             CHGVAR     VAR(&CUR_OSV_3) VALUE(%SST(&CUR_OSV 2 1) *CAT +
                                              %SST(&CUR_OSV 4 1) *CAT +
                                              %SST(&CUR_OSV 6 1))
CHGVAR VAR(&VERBOSE) VALUE(&OMISSIBLE)
MONMSG MSGID(MCH3601) EXEC(CHGVAR VAR(&VERBOSE) VALUE('N'))
CHGJOB JOBMSGQFL(*WRAP) 
IF COND(&DEVOPT = 'B') THEN(DO)
CHGVAR VAR(&SPOOL) VALUE(*PRINT)
CHGVAR VAR(&LOG)  VALUE(*SECLVL)
CHGVAR VAR(&LOGCLPGM) VALUE(*YES)
CHGVAR VAR(&DEVOPT) VALUE('N')
ENDDO
CRTSRCPF FILE(QTEMP/QDDSPF)
CHGVAR VAR(&TOMBR) VALUE('/QSYS.LIB/QTEMP.LIB/QDDSPF.FILE/GUIDE00F.MBR')
CALLPRC PRC('open') PARM((&TOMBR) (&OFLAG *BYVAL) (&MODE *BYVAL)) RTNVAL(&FD)
CHGVAR VAR(&ROW) VALUE('                R GUIDE')
CALLSUBR SUBR(EMIT80)
CHGVAR VAR(&ROW) VALUE('                  SRCFILE       10A')
CALLSUBR SUBR(EMIT80)
CHGVAR VAR(&ROW) VALUE('                  SRCMBR        10A')
CALLSUBR SUBR(EMIT80)
CHGVAR VAR(&ROW) VALUE('                  SRCTYPE       10A')
CALLSUBR SUBR(EMIT80)
CHGVAR VAR(&ROW) VALUE('                  SRCDESC       50A')
CALLSUBR SUBR(EMIT80)
CALLPRC PRC('close') PARM((&FD *BYVAL)) RTNVAL(&RTCD)
CRTPF FILE(QTEMP/GUIDE00F) SRCFILE(QTEMP/QDDSPF) OPTION(*NOSRC *NOLIST)
CHGVAR VAR(&FROMSTMF) VALUE(&FROMPATH *TCAT '/GUIDANCE.TXT')
CHGVAR VAR(&TOMBR) VALUE('/QSYS.LIB/QTEMP.LIB/QDDSPF.FILE/GUIDANCE.MBR')
CPYFRMSTMF FROMSTMF(&FROMSTMF) TOMBR(&TOMBR)
CPYF FROMFILE(QTEMP/QDDSPF) TOFILE(QTEMP/GUIDE00F) +
  FROMMBR(GUIDANCE) TOMBR(GUIDE00F) MBROPT(*ADD) FMTOPT(*CVTSRC)
DLTF FILE(QTEMP/QDDSPF)
IF COND(&VERBOSE *EQ 'L') +
   THEN(OVRDBF FILE(STDOUT) TOFILE(QPRINT) OVRSCOPE(*JOB) SHARE(*YES))

DOWHILE COND(&LOOP)
 RCVF
 MONMSG MSGID(CPF0864) EXEC(LEAVE)
 IF COND(&VERBOSE *EQ 'L') THEN(DO)
   IF COND(%SST(&SRCFILE 1 10) *EQ   '*PREREQ   ') THEN(DO)
     CHGVAR     VAR(&MSGDTA) VALUE( 'Comment: '      *CAT &SRCFILE *CAT +
                                    ' PackageName: ' *CAT &SRCMBR  *CAT +
                                    ' Options: '     *CAT &SRCTYPE *CAT +
                                    ' GitHub USER: ' *CAT &SRCDESC *CAT &X15 *CAT &NL)
   ENDDO
   ELSE DO
     IF COND(%SST(&SRCFILE 1 10) *EQ '*LANGUAGE ') THEN(DO)
       CHGVAR     VAR(&MSGDTA) VALUE( 'Comment: '      *CAT &SRCFILE *CAT +
                                      ' Language FC: ' *CAT &SRCMBR  *CAT +
                                      ' Options: '     *CAT &SRCTYPE *CAT +
                                      ' Description: ' *CAT &SRCDESC *CAT &X15 *CAT &NL)
     ENDDO
     ELSE DO
       IF COND(%SST(&SRCFILE 1 10) *EQ '*OSRELEASE') THEN(DO)
         CHGVAR     VAR(&MSGDTA) VALUE( 'Comment: '      *CAT &SRCFILE *CAT +
                                        '    IBM i OS: ' *CAT &SRCMBR  *CAT +
                                        ' Options: '     *CAT &SRCTYPE *CAT +
                                        ' Description: ' *CAT &SRCDESC *CAT &X15 *CAT &NL)
       ENDDO
       ELSE DO
         CHGVAR     VAR(&MSGDTA) VALUE( 'File: '       *CAT &SRCFILE *CAT +
                                        ' Member: '    *CAT &SRCMBR  *CAT +
                                        ' Attribute: ' *CAT &SRCTYPE *CAT +
                                        ' Text: '      *CAT &SRCDESC *CAT &X15 *CAT &NL)
       ENDDO                                 
     ENDDO                                 
   ENDDO                                 
   CALLPRC    PRC('printf') PARM((&MSGDTA))  
 ENDDO
 IF COND(%SST(&SRCFILE 1 10) *EQ '*PREREQ   ') THEN(DO)
   CHKOBJ OBJ(QSYS/&SRCMBR) OBJTYPE(*LIB) AUT(*READ)
   MONMSG MSGID(CPF9801 CPF9802) EXEC(DO)
     CHGVAR VAR(&MSGDTA) VALUE('Library ' *CAT &SRCMBR *TCAT ' does not exist or not authorized to')
     CALLPRC    PRC('printf') PARM((&MSGDTA))
     RETURN
   ENDDO  
 ENDDO
 IF COND(%SST(&SRCFILE 1 10) *EQ '*LANGUAGE ') THEN(DO)
   IF COND(%SST(&SRCMBR 1 4) *EQ &LANG_FC) THEN( +
     CHGVAR VAR(&LANG_DIR) VALUE('/' *CAT &LANG_FC))
   ITERATE
 ENDDO
 IF COND(%SST(&SRCFILE 1 10) *EQ '*OSRELEASE') THEN(DO)
   IF COND(%SST(&SRCMBR 1 6) *EQ &CUR_OSV) THEN( +
     CHGVAR VAR(&OS_DIR) VALUE('/' *CAT &CUR_OSV))
   ITERATE
 ENDDO
 IF COND(%SST(&SRCFILE 1 1) *EQ '*') THEN(ITERATE)
 CRTSRCPF FILE(QTEMP/&SRCFILE) RCDLEN(132)
 MONMSG MSGID(CPF0000)
 IF  COND((&LANG_DIR *NE '     ') *AND (&SRCFILE *EQ 'QCMDSRC   ')) THEN( +
                  CHGVAR VAR(&USELANGDIR) VALUE(&LANG_DIR))
 IF  COND((&OS_DIR *NE '       ') *AND (&SRCFILE *EQ 'QCLSRC   ')) THEN( +
                  CHGVAR VAR(&USEOSDIR) VALUE(&OS_DIR))
 CHGVAR VAR(&FROMSTMF) VALUE(&FROMPATH *TCAT '/' *CAT &SRCFILE *TCAT &USEOSDIR +
            *TCAT &USELANGDIR *TCAT '/' *CAT &SRCMBR *TCAT '.' *CAT &SRCTYPE)
 IF   COND(&USELANGDIR *NE '     ') THEN(CHGVAR VAR(&USELANGDIR) VALUE('     '))
 IF   COND(&USEOSDIR *NE '       ') THEN(CHGVAR VAR(&USEOSDIR) VALUE('       '))
                          
 CHGVAR VAR(&TOMBR) VALUE('/QSYS.LIB/QTEMP.LIB/' *CAT &SRCFILE +
                          *TCAT '.FILE/' *CAT &SRCMBR *TCAT '.MBR')
 CPYFRMSTMF FROMSTMF(&FROMSTMF) TOMBR(&TOMBR)
 MONMSG MSGID(CPFA095) EXEC(ITERATE)
 IF COND(&SRCTYPE *NE *BLANK) THEN(DO)
  CHGPFM FILE(QTEMP/&SRCFILE) MBR(&SRCMBR) SRCTYPE(&SRCTYPE)
  MONMSG MSGID(CPF0000 CPD0133)
  IF COND((&SRCFILE *EQ 'QCLSRC') *AND (&SRCTYPE *EQ 'CLLE')) THEN(DO)                             
   CHGVAR VAR(&QTMPMBR) VALUE('QTEMP/QCLSRC(' *CAT &SRCMBR *TCAT ')' *CAT &NL)                             
   CALLPRC PRC('_Ropen')   PARM(&QTMPMBR &IMODE) RTNVAL(&FP)                                                 
   CALLPRC PRC('_Rreadn')  PARM((&FP *BYVAL) &RAW_ROW (&F10_BUFLEN *BYVAL) (&OPTS *BYVAL)) RTNVAL(&RIOFB_P)  
   DOWHILE COND(&R04_NUMBYT *GT 0)                                                                           
     CHGVAR VAR(&SRCDTA) VALUE(%TRIML(&SRCDTA))                                                               
     IF (%SST(&SRCDTA 1 17) *EQ 'INCLUDE SRCSTMF(''') THEN(DO)                                                
       CHGVAR VAR(&IPOS) VALUE(%SCAN(&DOT &SRCDTA 18))                                                         
       IF COND(&IPOS *GT 0) THEN(DO)                                                                           
         CHGVAR VAR(&IPOS) VALUE(&IPOS - 18)                                                                    
         CHGVAR VAR(&SRCDTA) VALUE('INCLUDE SRCMBR(' *CAT %SST(&SRCDTA 18 &IPOS) *CAT ')')                      
         CALLPRC PRC('_Rupdate') PARM((&FP *BYVAL) &RAW_ROW (&F10_BUFLEN *BYVAL)) RTNVAL(&RIOFB_P)              
       ENDDO                                                                                                   
     ENDDO                                                                                                    
     CALLPRC PRC('_Rreadn')  PARM((&FP *BYVAL) &RAW_ROW (&F10_BUFLEN *BYVAL) (&OPTS *BYVAL)) RTNVAL(&RIOFB_P) 
   ENDDO                                                                                                     
   CALLPRC PRC('_Rfeod') PARM((&FP *BYVAL)) RTNVAL(&IRC)                                                                        
   CALLPRC PRC('_Rclose') PARM((&FP *BYVAL)) RTNVAL(&IRC)                                                                        
  ENDDO                                                                                                      
 ENDDO   
 IF COND(&SRCDESC *NE *BLANK) +  
   THEN(CHGPFM FILE(QTEMP/&SRCFILE) MBR(&SRCMBR) TEXT(&SRCDESC))
  
ENDDO
IF COND(&VERBOSE *EQ 'L') THEN(DO)
   CHGVAR     VAR(&MSGDTA) VALUE( 'Version: '    *CAT &CUR_OSV *CAT +
             '                     Language Feature: ' *CAT &LANG_FC *CAT &NL)
   CALLPRC    PRC('printf') PARM((&MSGDTA))
   DLTOVR     FILE(STDOUT) LVL(*JOB)
ENDDO
ADDLIBLE   LIB(TMKMAKE)
MONMSG MSGID(CPF0000)
CRTBNDCL PGM(QTEMP/BUILD) SRCFILE(QTEMP/QCLSRC) SRCMBR(BUILD) +
         OUTPUT(&SPOOL) ALWRTVSRC(*NO)
MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(FINE))
CHGJOB LOG(4 00 &LOG) LOGCLPGM(&LOGCLPGM)    
CALL PGM(QTEMP/BUILD) PARM(&DEVOPT &TGTRLS &TGTLIB)
MONMSG MSGID(CPF0000)

SUBR SUBR(EMIT80)
  CHGVAR VAR(&ROW) VALUE(%SST(&ROW 1 80) *CAT &LINEFEED)
  CALLPRC PRC('write') PARM((&FD *BYVAL) (&ROW) (&NOFB *BYVAL)) RTNVAL(&RTCD)
ENDSUBR

FINE: ENDPGM

