PGM PARM(&FROMPATH &TGTLIB &DEVOPT &TGTRLS &OMISSIBLE)
DCLPRCOPT  LOG(*NO) ALWRTVSRC(*NO)
COPYRIGHT  TEXT('Andrea Ribuoli Â© 2022 - PASERIE Local Inst')
DCL VAR(&FROMPATH) TYPE(*CHAR) LEN(100)
DCL VAR(&TGTLIB)    TYPE(*CHAR) LEN(10)
DCL VAR(&DEVOPT) TYPE(*CHAR) LEN(1)
DCL VAR(&TGTRLS)    TYPE(*CHAR) LEN(10)
DCL VAR(&OMISSIBLE) TYPE(*CHAR) LEN(1)
DCL VAR(&VERBOSE)   TYPE(*CHAR) LEN(1)
DCL VAR(&SPOOL)  TYPE(*CHAR) LEN(6) VALUE(*NONE)
DCL VAR(&LOOP) TYPE(*LGL) VALUE('1')
DCL VAR(&FROMSTMF) TYPE(*CHAR) LEN(150)
DCL VAR(&TOMBR) TYPE(*CHAR) LEN(150)
DCL VAR(&MODE) TYPE(*INT) LEN(4) VALUE(7)
DCL VAR(&OFLAG) TYPE(*INT) LEN(4) VALUE(16778570)
DCL VAR(&FD) TYPE(*INT) LEN(4)
DCL VAR(&NOFB) TYPE(*UINT) LEN(4) VALUE(81)
DCL VAR(&RTCD) TYPE(*INT) LEN(4)
DCL VAR(&ROW) TYPE(*CHAR) LEN(81)
DCL VAR(&NL) TYPE(*CHAR) LEN(1) VALUE(X'00')
DCL VAR(&X15) TYPE(*CHAR) LEN(1) VALUE(X'15')
DCL VAR(&LINEFEED) TYPE(*CHAR) LEN(1) VALUE(X'25')
DCL VAR(&MSGDTA) TYPE(*CHAR) LEN(128)
DCLF FILE(QTEMP/GUIDE00F)
CHGVAR VAR(&VERBOSE) VALUE(&OMISSIBLE)
MONMSG MSGID(MCH3601) EXEC(CHGVAR VAR(&VERBOSE) VALUE('N'))
IF COND(&DEVOPT = 'B') THEN(DO)
CHGVAR VAR(&SPOOL) VALUE(*PRINT)
CHGVAR VAR(&DEVOPT) VALUE('N')
ENDDO
CRTSRCPF FILE(QTEMP/QDDSPF)
CHGVAR VAR(&TOMBR) VALUE('/QSYS.LIB/QTEMP.LIB/QDDSPF.FILE/GUIDE00F.MBR')
CALLPRC PRC('open') PARM((&TOMBR) (&OFLAG *BYVAL) (&MODE *BYVAL)) RTNVAL(&FD)
CHGVAR VAR(&ROW) VALUE('                R GUIDE')
CHGVAR VAR(&ROW) VALUE(%SST(&ROW 1 80) *CAT &LINEFEED)
CALLPRC PRC('write') PARM((&FD *BYVAL) (&ROW) (&NOFB *BYVAL)) RTNVAL(&RTCD)
CHGVAR VAR(&ROW) VALUE('                  SRCFILE       10A')
CHGVAR VAR(&ROW) VALUE(%SST(&ROW 1 80) *CAT &LINEFEED)
CALLPRC PRC('write') PARM((&FD *BYVAL) (&ROW) (&NOFB *BYVAL)) RTNVAL(&RTCD)
CHGVAR VAR(&ROW) VALUE('                  SRCMBR        10A')
CHGVAR VAR(&ROW) VALUE(%SST(&ROW 1 80) *CAT &LINEFEED)
CALLPRC PRC('write') PARM((&FD *BYVAL) (&ROW) (&NOFB *BYVAL)) RTNVAL(&RTCD)
CHGVAR VAR(&ROW) VALUE('                  SRCTYPE       10A')
CHGVAR VAR(&ROW) VALUE(%SST(&ROW 1 80) *CAT &LINEFEED)
CALLPRC PRC('write') PARM((&FD *BYVAL) (&ROW) (&NOFB *BYVAL)) RTNVAL(&RTCD)
CHGVAR VAR(&ROW) VALUE('                  SRCDESC       50A')
CHGVAR VAR(&ROW) VALUE(%SST(&ROW 1 80) *CAT &LINEFEED)
CALLPRC PRC('write') PARM((&FD *BYVAL) (&ROW) (&NOFB *BYVAL)) RTNVAL(&RTCD)
CALLPRC PRC('close') PARM((&FD *BYVAL)) RTNVAL(&RTCD)
CRTPF FILE(QTEMP/GUIDE00F) SRCFILE(QTEMP/QDDSPF) OPTION(*NOSRC *NOLIST)
CHGVAR VAR(&FROMSTMF) VALUE(&FROMPATH *TCAT '/GUIDANCE.TXT')
CHGVAR VAR(&TOMBR) VALUE('/QSYS.LIB/QTEMP.LIB/QDDSPF.FILE/GUIDANCE.MBR')
CPYFRMSTMF FROMSTMF(&FROMSTMF) TOMBR(&TOMBR)
CPYF FROMFILE(QTEMP/QDDSPF) TOFILE(QTEMP/GUIDE00F) +
  FROMMBR(GUIDANCE) TOMBR(GUIDE00F) MBROPT(*ADD) FMTOPT(*CVTSRC)
DLTF FILE(QTEMP/QDDSPF)
IF COND(&VERBOSE *EQ 'L') +
   THEN(OVRDBF FILE(STDOUT) TOFILE(QPRINT) OVRSCOPE(*JOB) SHARE(*YES))

DOWHILE COND(&LOOP)
 RCVF
 MONMSG MSGID(CPF0864) EXEC(LEAVE)
 IF COND(&VERBOSE *EQ 'L') THEN(DO)
   IF COND(%SST(&SRCFILE 1 10) *EQ '*PREREQ   ') THEN(DO)
     CHGVAR     VAR(&MSGDTA) VALUE( 'Comment: '      *CAT &SRCFILE *CAT +
                                    ' PackageName: ' *CAT &SRCMBR  *CAT +
                                    ' Options: '     *CAT &SRCTYPE *CAT +
                                    ' GitHub USER: ' *CAT &SRCDESC *CAT &X15 *CAT &NL)
   ENDDO
   ELSE DO
     CHGVAR     VAR(&MSGDTA) VALUE( 'File: '       *CAT &SRCFILE *CAT +
                                    ' Member: '    *CAT &SRCMBR  *CAT +
                                    ' Attribute: ' *CAT &SRCTYPE *CAT +
                                    ' Text: '      *CAT &SRCDESC *CAT &X15 *CAT &NL)
   ENDDO                                 
  CALLPRC    PRC('printf') PARM((&MSGDTA))  
 ENDDO
 IF COND(%SST(&SRCFILE 1 10) *EQ '*PREREQ   ') THEN(DO)
   CHKOBJ OBJ(QSYS/&SRCMBR) OBJTYPE(*LIB) AUT(*READ)
   MONMSG MSGID(CPF9801 CPF9802) EXEC(DO)
     CHGVAR VAR(&MSGDTA) VALUE('Library ' *CAT &SRCMBR *TCAT ' does not exist or not authorized to')
     CALLPRC    PRC('printf') PARM((&MSGDTA))
     RETURN
   ENDDO  
 ENDDO
 IF COND(%SST(&SRCFILE 1 1) *EQ '*') THEN(ITERATE)
 CRTSRCPF FILE(QTEMP/&SRCFILE) RCDLEN(132)
 MONMSG MSGID(CPF0000)
 CHGVAR VAR(&FROMSTMF) VALUE(&FROMPATH *TCAT '/' *CAT &SRCFILE *TCAT '/' +
                           *CAT &SRCMBR *TCAT '.' *CAT &SRCTYPE)
 CHGVAR VAR(&TOMBR) VALUE('/QSYS.LIB/QTEMP.LIB/' *CAT &SRCFILE +
                          *TCAT '.FILE/' *CAT &SRCMBR *TCAT '.MBR')
 CPYFRMSTMF FROMSTMF(&FROMSTMF) TOMBR(&TOMBR)
 MONMSG MSGID(CPFA095) EXEC(ITERATE)
 IF COND(&SRCTYPE *NE *BLANK) THEN(DO)
  CHGPFM FILE(QTEMP/&SRCFILE) MBR(&SRCMBR) SRCTYPE(&SRCTYPE)
  MONMSG MSGID(CPF0000 CPD0133)
 ENDDO   
 IF COND(&SRCDESC *NE *BLANK) +  
   THEN(CHGPFM FILE(QTEMP/&SRCFILE) MBR(&SRCMBR) TEXT(&SRCDESC))
ENDDO
IF COND(&VERBOSE *EQ 'L') THEN(DO)
   DLTOVR     FILE(STDOUT) LVL(*JOB)
ENDDO

CRTBNDCL PGM(QTEMP/BUILD) SRCFILE(QTEMP/QCLSRC) SRCMBR(BUILD) +
         OUTPUT(&SPOOL) ALWRTVSRC(*NO)
MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(FINE))
IF COND(&TGTLIB *NE '*LOCALPATH') THEN(DO)
  CALL PGM(QTEMP/BUILD) PARM(&DEVOPT &TGTRLS &TGTLIB)
  MONMSG MSGID(CPF0000)  
ENDDO  
ELSE CMD(DO)
  CALL PGM(QTEMP/BUILD) PARM(&DEVOPT &TGTRLS)
  MONMSG MSGID(CPF0000)
ENDDO  
FINE: ENDPGM

